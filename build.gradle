plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.1'
    id 'io.spring.dependency-management' version '1.1.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
        vendor = JvmVendorSpec.ORACLE
    }
}

testing {
    suites {
        configureEach {
            useJUnitJupiter('5.9.3')
            dependencies {
                implementation 'org.springframework.boot:spring-boot-starter-test'
            }
        }

        integrationTest(JvmTestSuite) {
            testType = TestSuiteType.INTEGRATION_TEST

            dependencies {
                implementation project()
            }

            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    /* Web */
    implementation 'org.springframework.boot:spring-boot-starter-web'

    /* Actuator */
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    /* Database */
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.flywaydb:flyway-core'
    runtimeOnly 'com.h2database:h2'
}

tasks.named('check') {
    dependsOn(testing.suites.integrationTest)
}
